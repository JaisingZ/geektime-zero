buildscript {
    dependencies {
        classpath 'mysql:mysql-connector-java:8.0.15'
    }
}

plugins {
    id "org.flywaydb.flyway" version "5.2.4"
}

subprojects {
    apply plugin: 'java'

    group = 'com.github.dreamhead.zero'
    version = new Date().format('yyyyMMdd') + '-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked"
        options.compilerArgs << "-Xlint:deprecation"
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    ext {
        guavaVersion = '27.1-jre'
        springBootVersion = '2.1.3.RELEASE'

        lombokVersion = '1.18.6'

        mysqlVersion = '8.0.15'

        junitVersion = '5.4.0'
        hamcrestVersion = '2.1'
        assertJVersion = '3.11.1'
        mockitoVersion = '2.25.1'
    }

    dependencies {
        implementation("com.google.guava:guava:$guavaVersion")
        implementation("org.springframework.boot:spring-boot-starter:$springBootVersion")
        implementation("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
        implementation("org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion")

        compileOnly("org.projectlombok:lombok:$lombokVersion")
        annotationProcessor("org.projectlombok:lombok:$lombokVersion")

        runtime("mysql:mysql-connector-java:$mysqlVersion")

        testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
        testImplementation("org.junit.jupiter:junit-jupiter-params:$junitVersion")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
        testImplementation("org.assertj:assertj-core:$assertJVersion")
        testImplementation("org.mockito:mockito-core:$mockitoVersion")
    }

    test {
        useJUnitPlatform()
    }

    apply from: "$rootDir/gradle/config/scripts/style.gradle"
    apply from: "$rootDir/gradle/config/scripts/coverage.gradle"

    coverage.excludePackages = [
    ]

    coverage.excludeClasses = [
    ]

    style.excludePackages = [
    ]

    style.excludeClasses = [
    ]
}

configure(subprojects - project(':zero-bootstrap')) {
    project(':zero-bootstrap').dependencies {
        implementation(project)
    }
}

allprojects {
    apply plugin: 'idea'
}

apply from: "$rootDir/gradle/config/scripts/idea.gradle"

flyway {
    url = 'jdbc:mysql://localhost:3306/zero_test?useUnicode=true&characterEncoding=utf-8&useSSL=false&allowPublicKeyRetrieval=true&serverTimezone=GMT'
    user = 'zero'
    password = 'geektime'
    locations = ["filesystem:$rootDir/gradle/config/migration"]
}

wrapper {
    gradleVersion = '5.2.1'
}
